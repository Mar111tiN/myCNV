rule chrom_coverage:
    input:
        bam = get_bam_path
    output: 
        bedCov = "cnv/{sample}.{chrom}.cov"
    threads:
        config['CNV']['coverage']['threads']
    conda:
        f"../env/myCNV-CentOS-env.yml"
    script:
        "../scripts/coversnake.py"

rule hetero_SNP:
    input:
        bam = get_bam_path
    output: 
        bedCov = "cnv/{sample}.{chrom}.snp"
    threads:
        config['CNV']['hetSNP']['threads']
    conda:
        f"../env/myCNV-CentOS-env.yml"
    script:
        "../scripts/SNPsnake.py"

rule SNP2EB:
    input:
        table = "cnv/{sample}.{chrom}.snp",
        tumor_bam = get_bam_path
    output:
        "cnv/{sample}.{chrom}.snpEB"
    threads:
        config['EBFilter']['threads']['EBscore']
    conda:
        f"../env/eb-env.yml"
    script:
        "../scripts/ebsnake.py"


rule combine_CNV:
    input:
        snp = expand("cnv/{{sample}}.{chrom}.snp", chrom=chrom_list),
        cov = expand("cnv/{{sample}}.{chrom}.cov", chrom=chrom_list)
    output:
        snp = "tmp/{sample}.snp",
        cov = "CNV/{sample}.cov",
    threads:
        config['CNV']['combine']['threads']
    conda:
        f"../env/myCNV-CentOS-env.yml"
    script:
        "../scripts/combineCNVsnake.py"
    
rule rolling_cov:
    input:
        snp = "tmp/{sample}.snp",
        cov = "CNV/{sample}.cov"
    output:
        snp = "CNV/{sample}.snp",
        rcov = "CNV/{sample}.roll.cov" 
    threads:
        config['CNV']['combine']['threads']
    conda:
        f"../env/myCNV-CentOS-env.yml"
    script:
        "../scripts/rolling_coverage_snake.py"